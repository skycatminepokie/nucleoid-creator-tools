plugins {
  id 'fabric-loom' version '1.13.+'
  id 'maven-publish'
  id "com.modrinth.minotaur" version "2.+"
  id 'org.jreleaser' version '1.22.0'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_21

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
  accessWidenerPath = file("src/main/resources/creator_tools.accesswidener")
}

repositories {
  maven { url = "https://maven.nucleoid.xyz/" }
  maven { url = "https://maven.gegy.dev/" }
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }

}

dependencies {
  minecraft "com.mojang:minecraft:${project.minecraft_version}"
  mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
  modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

  modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

  modImplementation include('eu.pb4:polymer-core:0.15.1+1.21.11')
  modImplementation include('eu.pb4:polymer-virtual-entity:0.15.1+1.21.11')
  modImplementation include('xyz.nucleoid:server-translations-api:2.5.2+1.21.9-pre3') {
      exclude(group: "net.fabricmc.fabric-api")
  }
  modImplementation include('xyz.nucleoid:fantasy:0.7.0+1.21.11') {
      exclude(group: "net.fabricmc.fabric-api")
  }
  modImplementation(include('me.lucko:fabric-permissions-api:0.6.1'))

  modImplementation include('xyz.nucleoid:map-templates:0.2.2+1.21.10')
}

processResources {
  inputs.property "version", project.version

  filesMatching("fabric.mod.json") {
    expand "version": project.version
  }
}

tasks.withType(JavaCompile).configureEach {
  it.options.encoding = "UTF-8"
  it.options.release = 21
}

java {
  withJavadocJar()
  withSourcesJar()
}

jar {
  from("LICENSE") {
    rename { "${it}_${project.archivesBaseName}"}
  }
}

if (System.getenv("MODRINTH")) {
  modrinth {
    token = System.getenv("MODRINTH")
    projectId = 'HDocER98'// The ID of your modrinth project, slugs will not work.
    versionNumber = "" + version // The version of the mod to upload.
    versionType = "release"
    uploadFile = remapJar // This links to a task that builds your mod jar and sets "uploadFile" to the mod jar.
    gameVersions = [((String) project.minecraft_version)]
    loaders = ["fabric", "quilt"]
  }

  remapJar {
    finalizedBy project.tasks.modrinth
  }
}

jreleaser {
  signing {
    pgp {
      active = 'ALWAYS'
      armored = true
      mode = 'COMMAND'
    }
  }
  deploy {
    maven {
      mavenCentral {
        sontatype {
          active = 'RELEASE'
          url = 'https://central.sonatype.com/api/v1/publisher'
          stagingRepository('build/staging-deploy')
        }
      }
    }
  }
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId = group
      artifactId = archivesBaseName

      from components.java

      pom {
        name = archivesBaseName
        description = 'Tools for creators to make minigame content for the Nucleoid ecosystem'
        url = 'https://github.com/skycatminepokie/nucleoid-creator-tools'
        inceptionYear = '2026'
        licenses {
          license {
            name = 'LGPLv3'
            url = 'https://spdx.org/licenses/LGPL-3.0-only.html'
          }
        }
        developers {
          developer {
            id = 'skycatminepokie'
          }
          developer {
            id = 'Nucleoid Contributors'
            name = 'Nucleoid Contributors'
          }
        }
        scm {
          connection = 'scm:git:https://github.com/skycatminepokie/nucleoid-creator-tools.git'
          developerConnection = 'scm:git:ssh://github.com/skycatminepokie/nucleoid-creator-tools.git'
          url = 'http://github.com/skycatminepokie/nucleoid-creator-tools'
        }
      }
    }
  }

  repositories {
    maven {
      url = layout.buildDirectory.dir('staging-deploy')
    }
  }
}